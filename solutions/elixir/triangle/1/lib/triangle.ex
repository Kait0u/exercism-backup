defmodule Triangle do
  @type kind :: :equilateral | :isosceles | :scalene

  @msg_ineq_violation "side lengths violate triangle inequality"
  @msg_nonpositive_side "all side lengths must be positive"

  @doc """
  Return the kind of triangle of a triangle with 'a', 'b' and 'c' as lengths.
  """
  @spec kind(number, number, number) :: {:ok, kind} | {:error, String.t()}
  def kind(a, b, c)
    when a <= 0 or b <= 0 or c <= 0, do: {:error, @msg_nonpositive_side}

  def kind(a, b, c)
    when (a + b < c) or (b + c < a) or (a + c < b), do: {:error, @msg_ineq_violation}

  def kind(a, b, c) when a == b and b == c, do: {:ok, :equilateral}

  def kind(a, b, c) when a == b or a == c or b == c, do: {:ok, :isosceles}

  def kind(_a, _b, _c), do: {:ok, :scalene}
end
